#!/usr/bin/perl
#======================================================================
#					 D I S A S S E M B L E _ W P C 
#					 doc: Fri Mar 14 17:24:34 2025
#					 dlm: Fri Dec 26 22:25:46 2025
#					 (c) 2024 idealjoker@mailbox.org
#					 uE-Info: 114 45 NIL 0 0 72 2 2 4 NIL ofnI
#======================================================================

# WMS WPC Game Disassembly Instructions

# HISTORY:
#	Mar 14, 2025: - created from [disassemble_WPC_DMD]
#	Mar 18, 2025: - enabled disassemble_sycalls()
#	Mar 19, 2025: - adapted to free space autodection
#	Mar 25, 2025: - added -o
#				  - merged with [disassemble_WPC_DMD] and renamed
#	Apr  5, 2025: - BUG: start_page was not set correctly for Alpha games
#				  - added -p support
#				  - adapted to new obj output handling
#	Apr  6, 2025: - added -t)ag support
#	Apr 25, 2025: - added -f
#	Apr 26, 2025: - removed -f (made default)
#	May 29, 2025: - BUG: -f not removed everywhere
#				  - added gapBytes
#	Jun  8, 2025: - BUG: D711.pm error messages were suppressed
#	Jun 10, 2025: - BUG: DMD was not recognized
#	Jun 12, 2025: - corrected comments
#				  - added percentages to output
#	Jun 14, 2025: - added inCode ref detection
#	Jun 15, 2025: - adapted to change in [D711.WPC]
#				  - removed path from usage error
#	Jun 16, 2025: - BUG: usage error change prevented libs from being loaded
#	Jun 20, 2025: - added -v, reversed -s, and adapted to alpha and 89
#					version ids
#	Jun 21, 2025: - merged with studio version, had to re-do -s inversion
#	Jun 25, 2025: - added -w (gap warnings)
#	Jun 29, 2025: - added font  & DMD tables
#				  - BUG: alpha disassembled font and DMD tables
#	Aug 12, 2025: - keep up-to-date with changes in D711.WPC
#	Nov  6, 2025: - corrected some comments
#	Dec 26, 2025: - adapted to change in [D711.WPC]
#				  - added -D
# END OF HISTORY

#----------------------------------------------------------------------
# Usage
#----------------------------------------------------------------------

use Getopt::Std;
use lib ($0 =~ m@/[^/]*$@,$`);												# load [D711.pm]

my($prog)  = $0;
$prog =~ s@^.*/(.*)$@\1@;
die("Usage:\t$prog\n\t\t" .
			"[-i)nclude <file[,...]>\n\t\t" .
			"[include -a)ddressses in output] [include -c)ode bytes]\n\t\t" . 
			"[suppress -g)ap data (readability)]\n\t\t" . 
			"[produce gap -w)arnings (exploration)]\n\t\t" . 
			"[produce -s)tructured code]\n\t\t" .
			"[output -L)abel table]\n\t\t" .
			"[output -l)abel definitions]\n\t\t" .
			"[output -D)MD frames]\n\t\t" .
			"[output -o)bject data <PG|FF> [se-t) <tag>]] [-p)rocess object <file>]\n\t\t" .
			"-v <alpha|89> <ROM image>\n")
	unless &getopts('acDgi:lLo:p:st:v:w') && defined($opt_v) && @ARGV==1;

my($lbl);
if ($opt_v =~ m{^[aA]}) {
	$lbl = 'alpha';
	$D711::compilation_options .= '-SWPC:WPC_Version=alpha:ROM_Size=256KB';
} elsif ($opt_v =~ m{^8}) {
	$lbl = '89';
	$D711::compilation_options .= '-SWPC:WPC_Version=89:ROM_Size=512KB';
} else {
	die("Invalid WPC version <$opt_v>\n");
}
die if $@;

require "D711.pm";
D711->import("WPC($lbl)","\$opt_Q = $opt_l;");

$D711::print_addrs	=  $opt_a;												# -a) include hex addresses in output
$D711::print_code	=  $opt_c;												# -c) include code bytes in output
$D711::fill_gaps	= !$opt_g;												# -g) exclude gap data

#----------------------------------------------------------------------
# Load ROM
#----------------------------------------------------------------------

$D711::start_page = ($D711::WMS_System eq 'WPC(alpha)') ? 0x30 : 0x20;
&D711::load_ROM($ARGV[0],0x4000,$D711::start_page,16);						# Load ROM: first game page (256KB ROM)
&D711::load_ROM($ARGV[0],0x8000,0x3E,32);									# Load ROM: System pages
#printf(STDERR "Address Range = %04X - %04X\n",$D711::MIN_ROM_ADDR,$D711::MAX_ROM_ADDR);

#----------------------------------------------------------------------
# Disassemble ROM
#----------------------------------------------------------------------

D711::init_WPC(split(',',$opt_i));											# load API & -i args
$D711::verbose = !$opt_l;													# suppress disassembly abort output on -l (allLbls)

if (defined($opt_p)) {														# process object file
	D711::process_object_file($opt_p);			
	exit(0);
}

&D711::disassemble_syscalls();												# disassemble standard WPC stuff 
&D711::disassemble_CPU_vectors();
&D711::disassemble_system_pointers();
&D711::disassemble_lock_table();
unless ($D711::WMS_System eq 'WPC(alpha)') {
	&D711::disassemble_font_table();
	&D711::disassemble_DMD_frame_table($opt_D)
}

#----------------------------------------------------------------------
# Make Human Readable
#----------------------------------------------------------------------

D711::disassemble_inCode_refs(0xFF,0x8000,0xFFFF);							# LDX/B, LDY/A
D711::process_code_structure(0x8000,0xFFFF)									# if, while, ...
	if $opt_s;
D711::substitute_labels(0x8000,0xFFFF); 									# resolve labels
D711::substitute_identifiers(0x8000,0xFFFF);								# rename game-specific ids
D711::apply_magic_WPC(0x8000,0xFFFF); 		 	                            # e.g. &skip

for (my($rpg)=$D711::start_page; $rpg<0x3E; $rpg++) {
	D711::select_WPC_RPG($rpg,20);
	D711::disassemble_inCode_refs($rpg,0x4000,0x7FFF);	
	D711::process_code_structure(0x4000,0x7FFF)			
		if $opt_s;
	D711::substitute_labels(0x4000,0x7FFF); 			
	D711::substitute_identifiers(0x4000,0x7FFF);		
	D711::apply_magic_WPC(0x4000,0x7FFF); 				
}

if ($opt_w) {																# possible refs
	D711::produce_gap_warnings(0x8000,0xFFFF);
	for (my($rpg)=$D711::start_page; $rpg<0x3E; $rpg++) {
	    D711::select_WPC_RPG($rpg,20);
		D711::produce_gap_warnings(0x4000,0x7FFF);
    }
}
	

#----------------------------------------------------------------------
# Produce Output
#----------------------------------------------------------------------

if ($opt_L) {																# dump label information
	D711::dump_labels(0);													
	exit(0);																
}

if ($opt_l) {																# dump label information
	D711::dump_labels(1);													# fmt == 1 generates code that can be used with -i
	exit(0);																# to make a disassembled file with all labels defined up-front
}

exit(0) if ($opt_D);														# DMD frame output already produced during decoding
	

if (defined($opt_o)) {														# dump obj information from single page (or FF for prime real estate)
	if ($opt_o eq 'FF') {
		D711::produce_object_output($opt_o,0x8000,0xFFFF,$opt_t);
	} else {
		die("$0: invalid page id -o $opt_o\n")
			unless (hex($opt_o)>=$D711::start_page && hex($opt_o)<=0x3D);
		D711::select_WPC_RPG(hex($opt_o));
		D711::produce_object_output($opt_o,0x4000,0x7FFF,$opt_t);
	}
	exit(0);
}
		
#------------------------------
# Default C711 Source Output
#------------------------------

D711::select_WPC_RPG($D711::start_page,21);									# 1st game pg with headers
my($decoded) = D711::produce_output(0x4000,0x7FFF,1);
for (my($rpg)=$D711::start_page+1; $rpg<0x3E; $rpg++) {						# remaining game pages without headers
	D711::select_WPC_RPG($rpg,22);		
	$decoded += D711::produce_output(0x4000,0x7FFF,0);
}
$D711::_cur_RPG = 0xFF;														# allow free space to be found in system code
$decoded += D711::produce_output(0x8000,0xFFFF,0);							# WPC System

printf("; %5.1f KB (%.1f%%) decoded\n",
	$decoded/1024,int(0.5+100*$decoded/($decoded+$D711::gapBytes)));
printf("; %5.1f KB (%.1f%%) analysis gaps\n",
	$D711::gapBytes/1024,int(0.5+100*$D711::gapBytes/($decoded+$D711::gapBytes)))
		if ($D711::gapBytes);
printf("; %5.1f KB (%.1f%%) free space\n",
	$D711::freeBytes/1024,int(0.5+100*$D711::freeBytes/($decoded+$D711::gapBytes+$D711::freeBytes)))
		if ($D711::freeBytes);

exit(0);

